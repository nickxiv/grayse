program: block

block: line SEMICOLON
     | line SEMICOLON block

line: definition
    | expression
    | ifStatement
    | whileStatement
    | returnStatement

definition: varDefinition
          | funcDefinition
          | classDefinition

varDefinition: LET optVarAssignment

varList: VARIABLE
       | VARIABLE COMMA varList

optVarAssignment: varList GETS expression
                | varList

funcDefinition: FUNC VARIABLE OPENPARENTHESIS optVarList CLOSEPARENTHESIS OPENCURLY block CLOSECURLY

optVarList: varList
          | *empty*

funcCall: VARIABLE OPENPARENTHESIS optExpressionList CLOSEPARENTHESIS

classDefinition: CLASS VARIABLE OPENCURLY classProperties CLOSECURLY

classProperties: VARIABLE SEMICOLON
               | VARIABLE SEMICOLON classProperties

objProperties: propertyDefinition
             | propertyDefinition objProperties
             | *empty*

propertyDefinition: VARIABLE COLON unary SEMICOLON

parenExpressionList: OPENPARENTHESIS expression CLOSEPARENTHESIS


ifStatement: IF parenExpressionList OPENCURLY block CLOSECURLY optElse

optElse: ELSE OPENCURLY block CLOSECURLY
       | ELSE ifStatement
       | *empty*

array: OPENBRACKET optExpressionList CLOSEBRACKET

whileStatement: WHILE parenExpressionList OPENCURLY block CLOSECURLY

returnStatement: RETURN optExpressionList

optExpressionList: expressionList
                 | *empty*

expressionList: expression
              | expression COMMA expressionList

expression: unary
          | unary operator expression

unary: INTEGER
     | REAL
     | VARIABLE
     | TRUE
     | FALSE
     | STRING
     | funcCall
     | MINUS unary
     | array
     | parenExpressionList
     | OPENCURLY objProperties CLOSECURLY

operator: PLUS
        | PLUSPLUS
        | MINUS
        | MINUSMINUS
        | TIMES
        | DIVIDES
        | MOD
        | AND
        | OR
        | NOT
        | LESSTHAN
        | GREATERTHAN
        | ISEQUALTO
        | GETS
        | DOT
