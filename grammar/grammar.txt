program: block

block: line SEMICOLON
     | line SEMICOLON block

line: definition
    | expression
    | ifStatement
    | whileStatement
    | returnStatement

definition: varDefinition
          | funcDefinition
          | classDefinition

varDefinition: LET optVarAssignment

varList: VARIABLE
       | VARIABLE COMMA varList

optVarAssignment: varList GETS expression
                | varList

funcDefinition: FUNC VARIABLE OPENPARENTHESIS optVarList CLOSEPARENTHESIS OPENCURLY block CLOSECURLY

optVarList: varList
          | *empty*

funcCall: OPENPARENTHESIS optExpressionList CLOSEPARENTHESIS

classDefinition: CLASS VARIABLE OPENCURLY classProperties CLOSECURLY

classProperties: VARIABLE SEMICOLON
               | VARIABLE SEMICOLON classProperties

objProperties: propertyDefinition
             | propertyDefinition objProperties
             | *empty*

propertyDefinition: VARIABLE COLON unary SEMICOLON

ifStatement: IF OPENPARENTHESIS expression CLOSEPARENTHESIS OPENCURLY block CLOSECURLY optElse

optElse: ELSE OPENCURLY block CLOSECURLY
       | ELSE ifStatement
       | *empty*

array: OPENBRACKET optExpressionList CLOSEBRACKET

whileStatement: WHILE OPENPARENTHESIS expression CLOSEPARENTHESIS OPENCURLY block CLOSECURLY

returnStatement: RETURN optExpression

optExpression: expression
             | *empty*

optExpressionList: expressionList
                 | *empty*

expressionList: expression
              | expression COMMA expressionList

expression: unary
          | unary operator expression

unary: INTEGER
     | REAL
     | uVariable
     | TRUE
     | FALSE
     | STRING
     | MINUS unary
     | array
     | NOT
     | OPENPARENTHESIS expression CLOSEPARENTHESIS
     | LAMBDA OPAREN optExpressionList CPAREN  OPENCURLY block CLOSECURLY
     | OPENCURLY objProperties CLOSECURLY

uVariable: VARIABLE
         | VARIABLE funcCall

operator: PLUS
        | MINUS
        | TIMES
        | DIVIDES
        | MOD
        | AND
        | OR
        | LESSTHAN
        | GREATERTHAN
        | ISEQUALTO
        | GETS
        | DOT
